plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.27.1'
    id 'com.gradle.plugin-publish' version '0.16.0'
}

project.description 'Gradle project versioning plugin based on Git tags.'
project.group = 'com.productmadness'
project.version = '1.0.5'

sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
}

ext {
    isArtifactoryEnabled = project.hasProperty('artifactory')
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

if (isArtifactoryEnabled) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name "jfrog"
                url "${artifactory_context_url}/${artifactory_plugins_repo_key}"
                credentials {
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
            }
        }
    }

    artifactory {
        contextUrl = "${artifactory_context_url}"
        publish {
            repository {
                repoKey = "${artifactory_plugins_repo_key}"
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = false
            }
        }
    }
}

if (!isArtifactoryEnabled) {
    build.finalizedBy(publishToMavenLocal)
}

gradlePlugin {
    plugins {
        versioningPlugin {
            id = 'com.productmadness.versioning'
            displayName = 'Versioning'
            description = 'Gradle project versioning plugin based on Git tags.'
            implementationClass = 'com.productmadness.plugin.VersioningPlugin'
        }
    }
}
